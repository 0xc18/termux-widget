apply plugin: 'com.android.application'

android {
    compileSdkVersion project.properties.compileSdkVersion.toInteger()
    defaultConfig {
        applicationId "com.termux.widget"
        minSdkVersion project.properties.minSdkVersion.toInteger()
        targetSdkVersion project.properties.targetSdkVersion.toInteger()
        versionCode 12
        versionName "0.12"

        manifestPlaceholders.TERMUX_PACKAGE_NAME = "com.termux"
        manifestPlaceholders.TERMUX_APP_NAME = "Termux"
        manifestPlaceholders.TERMUX_WIDGET_APP_NAME = "Termux:Widget"
    }

    signingConfigs {
        debug {
            storeFile file('dev_keystore.jks')
            keyAlias 'alias'
            storePassword 'xrj45yWGLbsO7W0v'
            keyPassword 'xrj45yWGLbsO7W0v'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false // Reproducible builds
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            zipAlignEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "debug") {
                def releaseTag = System.getenv("RELEASE_TAG")
                outputFileName = new File("termux-widget" + (releaseTag ? "-" + releaseTag : "") + "-debug.apk")
            }
        }
    }
}

task versionName {
  doLast {
    print android.defaultConfig.versionName
  }
}

dependencies {
    // required for TermuxWidgetControlsProviderService
    implementation "org.reactivestreams:reactive-streams:1.0.3"
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'

    implementation 'com.termux.termux-app:termux-shared:23b707a819'

    // Use if below libraries are published locally by termux-app with `./gradlew publishReleasePublicationToMavenLocal` and used with `mavenLocal()`.
    // If updates are done, republish there and sync project with gradle files here
    //implementation 'com.termux:termux-shared:0.117'
}
